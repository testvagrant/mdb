group 'com.testvagrant.mdb'
version '2.2.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'


sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'org.testng', name: 'testng', version: '6.11'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.1'
    testCompile "org.mockito:mockito-core:2.8.47"
    compile('com.testvagrant.optimus-commons:optimus-commons:2.2.1')

}


test {
    // enable TestNG support (default is JUnit)
    useTestNG()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


group = "com.testvagrant.mdb"
archivesBaseName = "mdb"
version = "2.2.1"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'MDB'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Mobile debug bridge, an interface that allows iOS or Android device interaction'
                url 'https://github.com/testvagrant/mdb'

                scm {
                    connection 'scm:https://github.com/testvagrant/mdb.git'
                    url 'https://github.com/testvagrant/mdb'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'sukesh'
                        name 'Sukesh Kumar'
                        email 'sukesh@testvagrant.com'
                    }
                }
            }
        }
    }
}
